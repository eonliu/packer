plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.eonliu.packer'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.eonliu.packer.demo"
        minSdk 24
        targetSdk 31
        versionCode 1
        versionName "1.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        flavorDimensions "versionCode"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        custom {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'com.meituan.android.walle:library:1.1.7'
}

packer {

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    jiagu {
        userName = properties.getProperty('packerUserName')
        password = properties.getProperty('packerPassword')
        jiaguPath =  properties.getProperty('packerJiaguPath')// 360加固工具路径
//        channelsPath = "${project.projectDir}/channels.txt"
        channelsPath = "${project.projectDir}/walle_channels"
        useWalle = true // 默认使用360加固，如果为true则使用美团walle进行多渠道打包
    }

    sign {
        keystorePath = "${project.projectDir}/packer-demo.keystore"
        keystorePassword = "packer123"
        alias = "packer"
        aliasPassword = "packer123"
    }

    ftp {
        ftpUserName = properties.getProperty('packerFtpUserName')
        ftpPassword = properties.getProperty('packerFtpPassword')
        ftpUrl = properties.getProperty('packerFtpUrl')
        autoCreateDir = true
        publishDir = "packer-demo-release"
        uploadDir = "packer-demo-beta"
        uploadMapping = true // 是否上传build/outputs/mapping文件，默认上传
        uploadLogs = true // 是否上传build/outputs/logs，默认上传
        uploadSdkDependencies = true // 是否上传build/outputs/sdk-dependencies，默认上传
    }

}